plugins {
    id 'java'
}

group 'com.demoblaze'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    // Cucumber
    implementation 'io.cucumber:cucumber-java:7.11.1'
    implementation 'io.cucumber:cucumber-junit:7.11.1'
    
    // RestAssured untuk API Testing
    implementation 'io.rest-assured:rest-assured:5.3.0'
    implementation 'io.rest-assured:json-schema-validator:5.3.0'
    
    // Selenium untuk Web UI Testing
    implementation 'org.seleniumhq.selenium:selenium-java:4.9.0'
    implementation 'io.github.bonigarcia:webdrivermanager:5.3.2'
    
    // Untuk pengolahan JSON
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    
    // JUnit
    testImplementation 'junit:junit:4.13.2'
    
    // Faker untuk data testing
    implementation 'com.github.javafaker:javafaker:1.0.2'
    
    // AssertJ untuk Assertion yang lebih ekspresif
    implementation 'org.assertj:assertj-core:3.24.2'
}

// Task untuk menjalankan test API
task apiTests() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.testRuntimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--plugin', 'html:build/reports/cucumber/api/index.html',
                    '--plugin', 'json:build/reports/cucumber/api/cucumber.json',
                    '--glue', 'com.demoblaze.stepdefinitions.api',
                    '--tags', '@api',
                    'src/test/resources/features/api']
        }
    }
}

// Task untuk menjalankan test Web UI
task webTests() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.testRuntimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty',
                    '--plugin', 'html:build/reports/cucumber/web/index.html',
                    '--plugin', 'json:build/reports/cucumber/web/cucumber.json',
                    '--glue', 'com.demoblaze.stepdefinitions.web',
                    '--tags', '@web',
                    'src/test/resources/features/web']
        }
    }
}

test {
    useJUnit()
    testLogging {
        events "passed", "skipped", "failed"
    }
}